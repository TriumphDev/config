plugins {
    id 'java-library'
    id 'maven-publish'
}

group "me.mattstudios"
version "1.0.0-SNAPSHOT"

dependencies {
    api "org.yaml:snakeyaml:1.27"
    compileOnly "org.jetbrains:annotations:20.0.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = "ConfigMe fork that changes many core features"
                description = "Adds custom mappers, changed property initializer style, etc.."
                url = "https://github.com/TriumphDev/triumph-config"

                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }

                developers {
                    developer {
                        id = "matt"
                        name = "Mateus Moreira"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/TriumphDev/triumph-config.git"
                    developerConnection = "scm:git:ssh://github.com:TriumphDev/triumph-config.git"
                    url = "http://github.com/TriumphDev/triumph-config"
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username repoUsername
                password repoPassword
            }

            url = repoUrl
        }
    }

}
